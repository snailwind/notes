## 条件语句：

　　又称选择语句，它的主要作用是根据条件的变化选择执行不同的代码，它包括`IF语句`和`CASE语句`。

### 1. IF … THEN … END IF;

```SQL
IF <condition expression> THEN
   plsql_sentence
END IF;
```

```SQL
DECLARE
  V_TYPE VARCHAR2(3) := UPPER('&请指定计算类型');
  V_SAL  NUMBER;
  V_SQL  VARCHAR2(1000);
BEGIN
  IF V_TYPE IN('MAX','MIN','SUM','AVG') THEN
     V_SQL := 'SELECT '||V_TYPE||'(SAL) FROM EMP';
    EXECUTE IMMEDIATE V_SQL INTO V_SAL;
    DBMS_OUTPUT.PUT_LINE('所有员工的'||CASE WHEN V_TYPE = 'MAX' THEN '最大' 
                                            WHEN V_TYPE = 'MIN' THEN '最小' 
                                            WHEN V_TYPE = 'SUM' THEN '合计' 
                                            WHEN V_TYPE = 'AVG' THEN '平均'
                                       END
                         ||'薪资是'||ROUND(V_SAL)||'元');
  END IF;
END;
```

　--判断指定员工是否在职，若在职，辞退;否则，不做处理

　--辞退的员工，打印:员工XXXX(姓名)被辞退

```SQL
DECLARE 
  V_EMPNO NUMBER(4) := &请指定工号;
  V_CT    NUMBER;
  V_ENAME VARCHAR2(10);
BEGIN 
  SELECT COUNT(1) INTO V_CT FROM EMP WHERE EMPNO = V_EMPNO;
  IF V_CT = 1 THEN
    SELECT ENAME INTO V_ENAME FROM EMP WHERE EMPNO=V_EMPNO;
    DELETE FROM EMP WHERE EMPNO = V_EMPNO;
    COMMIT;
    DBMS_OUTPUT.PUT_LINE('员工'||V_ENAME||'被辞退');
  END IF;
END;
```

### 2. IF … THEN … ELSE … END IF;

```SQL
IF <condition expression> THEN
  plsql_sentence1
ELSE 
  plsql_sentence2 
END IF;
```



```SQL
DECLARE
  V_TYPE VARCHAR2(3) := UPPER('&请指定计算类型');
  V_SAL  NUMBER;
  V_SQL  VARCHAR2(1000);
BEGIN
  IF V_TYPE IN('MAX','MIN','SUM','AVG') THEN
     V_SQL := 'SELECT '||V_TYPE||'(SAL) FROM EMP';
    EXECUTE IMMEDIATE V_SQL INTO V_SAL;
    DBMS_OUTPUT.PUT_LINE('所有员工的'||CASE WHEN V_TYPE = 'MAX' THEN '最大' 
                                            WHEN V_TYPE = 'MIN' THEN '最小' 
                                            WHEN V_TYPE = 'SUM' THEN '合计' 
                                            WHEN V_TYPE = 'AVG' THEN '平均'
                                       END
                         ||'薪资是'||ROUND(V_SAL)||'元');
  ELSE 
    DBMS_OUTPUT.PUT_LINE('请正确指定计算类型（MAX/MIN/SUM/AVG）');
  END IF;
END;
```



```SQL
DECLARE 
  V_EMPNO NUMBER(4) := &请指定工号;
  V_CT    NUMBER;
  V_ENAME VARCHAR2(10);
BEGIN 
  SELECT COUNT(1) INTO V_CT FROM EMP WHERE EMPNO = V_EMPNO;
  IF V_CT = 1 THEN
    SELECT ENAME INTO V_ENAME FROM EMP WHERE EMPNO = V_EMPNO;
    DELETE FROM EMP WHERE EMPNO = V_EMPNO;
    COMMIT;
    DBMS_OUTPUT.PUT_LINE('员工'||V_ENAME||'被辞退');
  ELSE 
    DBMS_OUTPUT.PUT_LINE('该员工不存在，请确认');
  END IF;
END;
```



### 3. IF … THEN … ELSIF … END IF;

```SQL
IF 
  <condition expression1> THEN
  plsql_sentence1
ELSIF 
  <condition expression2> THEN
  plsql_sentence2 
  ...
ELSE 
  plsql_sentencen 
END IF;
```

　--指定部门编号，
　　--如果该部门存在且有人，删除该部门下所有员工，并打印：“己整体辞退”;
　　--如果部门存在但没有人，打印：“该部门下没有人”;
　　--如果该部门不存在，打印：“部门不存在”。

```SQL
DECLARE 
  V_DEPTNO NUMBER(2) := &请输入部门编号;
  V_CT     NUMBER;
BEGIN 
  SELECT COUNT(1) INTO V_CT FROM DEPT WHERE DEPTNO = V_DEPTNO;
  IF V_CT = 1 THEN 
    SELECT COUNT(1) INTO V_CT FROM EMP WHERE DEPTNO = V_DEPTNO;
    IF V_CT > 0 THEN
      DELETE FROM EMP WHERE DEPTNO = V_DEPTNO;
      COMMIT;
      DBMS_OUTPUT.PUT_LINE('部门'||V_DEPTNO||'己整体辞退');
    ELSE 
      DBMS_OUTPUT.PUT_LINE('部门'||V_DEPTNO||'下没有人');
    END IF;
  ELSE 
    DBMS_OUTPUT.PUT_LINE('部门'||V_DEPTNO||'不存在');
  END IF;
END;
```



```SQL
DECLARE 
  V_DEPTNO  NUMBER(2) := &请输入部门编号;
  V_CT1     NUMBER ;
  V_CT2     NUMBER ;
BEGIN 
  SELECT COUNT(*) INTO V_CT1 FROM DEPT WHERE DEPTNO = V_DEPTNO;
  SELECT COUNT(*) INTO V_CT2 FROM EMP WHERE DEPTNO = V_DEPTNO;
  IF V_CT1 = 1 AND V_CT2 > 0 THEN 
     DELETE FROM EMP WHERE DEPTNO = V_DEPTNO;
     COMMIT;
     DBMS_OUTPUT.PUT_LINE('部门'||V_DEPTNO||'己整体辞退');
  ELSIF V_CT1 = 1 AND V_CT2 = 0 THEN
     DBMS_OUTPUT.PUT_LINE('部门'||V_DEPTNO||'下没有人');
  ELSE 
     DBMS_OUTPUT.PUT_LINE('部门'||V_DEPTNO||'不存在');
  END IF;
END;
```



### 4. CASE WHEN…

```SQL
CASE WHEN <condition expression1> THEN
          plsql_sentence1
     WHEN <condition expression2> THEN
          plsql_sentence2 
     ...
     ELSE plsql_sentencen 
 END CASE;
```



```SQL
DECLARE
  V_GRADE CHAR(1) := UPPER('&P_G');
BEGIN
  CASE
    WHEN V_GRADE = 'A' THEN
      DBMS_OUTPUT.PUT_LINE('优');
    WHEN V_GRADE = 'B' THEN
      DBMS_OUTPUT.PUT_LINE('良');
    WHEN V_GRADE = 'C' THEN
      DBMS_OUTPUT.PUT_LINE('中');
    WHEN V_GRADE = 'D' THEN
      DBMS_OUTPUT.PUT_LINE('差');
    ELSE
      DBMS_OUTPUT.PUT_LINE('劝退');
  END CASE;
END;

DECLARE
  V_GRADE CHAR(1) := UPPER('&P_G');
  V_DES   VARCHAR2(250);
BEGIN
  V_DES := CASE V_GRADE
             WHEN 'A' THEN '优'
             WHEN 'B' THEN '良'
             WHEN 'C' THEN '中'
             WHEN 'D' THEN '差'
             ELSE '劝退'
           END;
  DBMS_OUTPUT.PUT_LINE(V_DES);
END;
```

　--指定员工工号，
　　--如果是10号部门的，薪资+1000，
　　--如果是20号部门的，薪资+2000，
　　--如果是30号部门的，薪资+3000。

```SQL
SELECT * FROM EMP;
```

　--`CASE WHEN`用于语句内部，组成`DML`或`SELECT`语句，`THEN`后面跟数据

```SQL
DECLARE
  V_EMPNO  NUMBER(4) := &指定工号;
BEGIN
  UPDATE EMP SET SAL = CASE WHEN DEPTNO = 10 THEN SAL+1000
                            WHEN DEPTNO = 20 THEN SAL+2000
                            WHEN DEPTNO = 30 THEN SAL+3000
                        END
    WHERE EMPNO = V_EMPNO;
  COMMIT;
END;
```

　---`CASE WHEN`用于流程控制，不组成语句本身，`THEN`后边跟代码

```SQL
DECLARE
  V_EMPNO  NUMBER(4):= &指定工号;
  V_DEPTNO NUMBER(2);
BEGIN
  SELECT DEPTNO INTO V_DEPTNO FROM EMP WHERE EMPNO = V_EMPNO;
  CASE WHEN V_DEPTNO = 10 THEN
         UPDATE EMP SET SAL =SAL+1000 WHERE EMPNO = V_EMPNO;
       WHEN V_DEPTNO = 20 THEN
         UPDATE EMP SET SAL=SAL+2000 WHERE EMPNO = V_EMPNO;
       WHEN V_DEPTNO = 30 THEN
         UPDATE EMP SET SAL= SAL+3000 WHERE EMPNO = V_EMPNO;
  END CASE;
  COMMIT;
END;
```



### 条件语句-注意点

1. 所有`IF类型`的条件语句，最后要加上`END IF`；所有`CASE类型`的条件语句，最后要加上`END CASE`；
2. 如果判断条件很多，后一个条件被判定的前提是前一个条件结果为`false`；
3. `ELSE`部分可根据实际需求决定是否书写；
4. `CASE语句`可以在`CASE`后写选择器，在`WHEN`后写常量，通过比较选择器与常量，判断结果是`true`还是`false`；
5. 条件语句中可以继续嵌套条件语句，但不建议嵌套太多层；
6. 条件语句的最后和条件语句中的执行部分，最后都要按要求添加英文分号。



