### 1. 存储函数

- 在SQL基础中已经提到过函数，当时对于函数的分类中有一种是分为**“系统函数”**和**“自定义函数”**，之前所学都是系统自带的函数，而这里的存储函数即自定义函数。
- 函数本身的存在意义即是解决某些计算或功能如何更易实现，对于绝大多数人的共同需求造就了**系统函数**，少部分人的需求就出现了**自定义函数**。
- 相对于**存储过程**，**存储函数**的数量上、使用频率上都相对较低，源于**存储函数**必须要有**返回值**，而且是**单一返回值**，所以除非特殊功能，多数功能都采用过程来实现。



#### 1. 存储函数-创建

```SQL
CREATE [OR REPLACE] FUNCTION fun_name [(paral,para2...)]
RETURN datatype
AS|IS
  varname var_type;
  ...
BEGIN
  plsql_sentence
  RETURN var_name;
  EXCEPTION
    except_sentence
END [fun_name];
```



```SQL
--设计一个存储函数，用于计算每个员工司龄
CREATE OR REPLACE FUNCTION F_HIREDATE(I_HIREDATE DATE) 
RETURN NUMBER 
IS
BEGIN
  RETURN TO_CHAR(SYSDATE,'YYYY') - TO_CHAR(I_HIREDATE,'YYYY');
END F_HIREDATE;

SELECT ENAME,F_HIREDATE(HIREDATE) FROM EMP;
```



```SQL
--查询年份是否闰年
CREATE OR REPLACE FUNCTION F_YEAR(I_YEAR IN NUMBER) 
RETURN VARCHAR2 
IS
  V_TYPE VARCHAR2(100);
BEGIN
  IF MOD(I_YEAR,4) = 0 AND MOD(I_YEAR,100) <> 0 OR MOD(I_YEAR,400) = 0 THEN
    V_TYPE := '闰年';
  ELSE
    V_TYPE := '平年';
  END IF;
  RETURN V_TYPE;
END F_YEAR;

SELECT F_YEAR(2020) FROM DUAL;
```



```SQL
--创建一个函数，调用时输入薪资和奖金，返回年收入
CREATE OR REPLACE FUNCTION F_YSAL(I_SAL NUMBER,I_COMM NUMBER) 
RETURN NUMBER 
IS
BEGIN 
  RETURN (NVL(I_SAL,0)+NVL(I_COMM,0))*12;
END F_YSAL;

SELECT ENAME,SAL,COMM,F_YSAL(SAL,COMM) FROM EMP;
```



　存储函数的创建语法与存储过程非常相似，区别有如下内容：

- 1）函数的关键词为**FUNCTION**；
- 2）函数几乎不用输出参数，只有**输入参数**和一个**返回值类型**，这个返回值类型要使用`RETURN`关键词单独声明，没有名字，没有长度；
- 3）程序中要使用`RETURN`关键词指明待返回结果，这个待返回结果要与声明的**返回值类型**一致。



#### 2. 存储函数-编译

- 与**存储过程**一样，**存储函数**在创建后的**执行**只是将该程序**写入数据库**中，并非真正调用了它。
- 在程序**编译**后，会在`FUNCTION`路径下出现该**函数**的名字，并且有问题的**函数**上会出现`X`号进行**标识**。
- 对于有问题的**函数**，可以在`view`或`edit`界面进行原因查看和语法调试。



#### 3. 存储函数-调用

　　**存储函数**的调用方法与普通的**系统函数**无异，只要知晓函数的用途和用法即可。

```SQL
SELECT fun_name(para) FROM DUAL;
```



#### 4. 存储函数-删除

语法：

```SQL
DROP FUNCTION fun_name;
```

同存储过程一样，实际项目中，存储函数也是**很少删除**，基本都是被**替换**或被**遗弃**。



#### 5.存储函数递归调用

```SQL
CREATE OR REPLACE FUNCTION FN(N NUMBER)
RETURN NUMBER
IS
BEGIN
  IF N<=2 THEN
    RETURN N;
  ELSE
    RETURN N*FN(N-1);
  END IF;
END;
```

EMP表:要求查询出EMP表中一个员工的上级名称，查询的格式如下：
例如：KING-MANAGER-GROUP-SMITH
KING,MANGER,GROUP,SMITH
使用递归调用

```SQL
CREATE OR REPLACE FUNCTION FN2(ENO NUMBER)
RETURN VARCHAR2
IS
   --声名一个EMP%ROWTYPE类型的变量
   V EMP%ROWTYPE;
BEGIN
   /*
   FN2(7369)
   查询出员工的姓名 V.ENAME=SMITH V.MGR=7902 判断V.MGR是否为空，条件不成立 FN2(7902)||','||SMITH
   FN2(7902)
   ...............V.ENAME=FORD V.MGR=7566    ............................. FN2(7566)||','||FORD
   FN2(7566)
   ...............V.ENAME=JONES V.MGR=7839   ............................. FN2(7839)||','||JONES
   FN2(7839)
   ...............V.ENAME=KING V.MGR=NULL    ...................条件成立   KING
   
   KING,JONES,FORD,SMITH
   */
   --根据员工编号查询员工的姓名和员工的上级编号
   SELECT * INTO V FROM EMP WHERE EMPNO=ENO;
   IF V.MGR IS NULL THEN
     RETURN V.ENAME;
   ELSE
     --继续查询该员工上级
     RETURN FN2(V.MGR)||','||V.ENAME;
   END IF;
END;

SELECT FN2(7369) FROM DUAL;
KING,JONES,FORD,SMITH
```

##### 层次查询(递归查询)

适合查询有层次关系的数据

```SQL
LEVEL:表示层级序号
SELECT EMP.*,LEVEL FROM EMP CONNECT BY PRIOR MGR=EMPNO START WITH EMPNO=7369;
START WITH 条件:指定第一条数据
CONNECT BY PRIOR: 关联条件
SELECT EMP.*,LEVEL FROM EMP CONNECT BY PRIOR EMPNO=MGR START WITH MGR IS NULL;
SELECT * FROM EMP;
```



### 2. 程序包

　　**程序包**，简称**包**，是**PL/SQL**中各种程序的汇总。
　　项目上线前常常被要求程序打包，某些情况下的打包就是将各种要投产使用的过程、函数等汇入到一个程序包中。

　　实际上在之前的学习中就有提到过的`DBMS_OUTPUT`即是一个**程序包**，其中的`PUT_LINE`是**程序包**中的一个**过程**。

　　包的组成分为**规范**和**包主体**，俗称**包头**和**包体**。



#### 1. 包头

```SQL
CREATE OR REPLACE PACKAGE pkg_name IS
  --过程的名字和参数信息
  PROCEDURE pro_name(paral,para2.. ) ;
  --函数的名字和参数信息
  FUNCTION fun_name(paral,para2..) return data_type;
  ...
END pkg_name;
```



#### 2. 包体

```SQL
CREATE OR REPLACE PACKAGE BODY pkg_name IS
  --过程的具体内容
  PROCEDURE pro_name (paral,para2..) is ...
  --程序的具体内容
  FUNCTION fun_name (para1,para2..) return data_type is ...;
  ...
END pkg_name;
```



```SQL
--包头：简介                              
CREATE OR REPLACE PACKAGE PKG_73 IS
PROCEDURE SP_REBUILD_EMP;
PROCEDURE SP_YEAR(I_YEAR IN NUMBER,    --输入类型：IN
                  O_YEAR OUT VARCHAR2);  --输出类型：OUT,如果此处的IN和OUT都不写，则全部默认为输入类型
FUNCTION F_SUM (A NUMBER,B NUMBER)
                RETURN NUMBER;
END PKG_73;

--包体，详细内容
CREATE OR REPLACE PACKAGE BODY PKG_73 IS
  --第一个程序
  PROCEDURE SP_REBUILD_EMP 
  AUTHID CURRENT_USER IS
    V_CT NUMBER;
  BEGIN
    --STEP1:删除表
    BEGIN
      SELECT COUNT(1) INTO V_CT FROM USER_TABLES WHERE TABLE_NAME = 'EMP' ;
      IF V_CT=1 THEN
        EXECUTE IMMEDIATE 'DROP TABLE EMP';
      END IF;
    END;
    DBMS_OUTPUT.PUT_LINE('STEP1,完成');
    --STEP2:建表
    BEGIN 
      EXECUTE IMMEDIATE 'CREATE TABLE EMP
       (EMPNO    NUMBER(4) NOT NULL,
        ENAME    VARCHAR2(10),
        JOB      VARCHAR2(9),
        MGR      NUMBER(4),
        HIREDATE DATE,
        SAL      NUMBER(7,2),
        COMM     NUMBER(7,2),
        DEPTNO   NUMBER(2))';
    END;
    DBMS_OUTPUT.PUT_LINE('STEP2,完成');
    --STEP3:添加数据
    BEGIN 
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7369, ''SMITH'', ''CLERK'', 7902, TO_DATE(''17-12-1980'', ''dd-mm-yyyy''), 800.00, NULL, 20)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7499, ''ALLEN'', ''SALESMAN'', 7698, TO_DATE(''20-02-1981'', ''dd-mm-yyyy''), 1600.00, 300.00, 30)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7522, ''WARD'', ''SALESMAN'', 7698, TO_DATE(''22-02-1981'', ''dd-mm-yyyy''), 1250.00, 500.00, 30)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7566, ''JONES'', ''MANAGER'', 7839, TO_DATE(''02-04-1981'', ''dd-mm-yyyy''), 2975.00, NULL, 20)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7654, ''MARTIN'', ''SALESMAN'', 7698, TO_DATE(''28-09-1981'', ''dd-mm-yyyy''), 1250.00, 1400.00, 30)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7698, ''BLAKE'', ''MANAGER'', 7839, TO_DATE(''01-05-1981'', ''dd-mm-yyyy''), 2850.00, NULL, 30)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7782, ''CLARK'', ''MANAGER'', 7839, TO_DATE(''09-06-1981'', ''dd-mm-yyyy''), 2450.00, NULL, 10)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7788, ''SCOTT'', ''ANALYST'', 7566, TO_DATE(''19-04-1987'', ''dd-mm-yyyy''), 3000.00, NULL, 20)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7839, ''KING'', ''PRESIDENT'', NULL, TO_DATE(''17-11-1981'', ''dd-mm-yyyy''), 5000.00, NULL, 10)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7844, ''TURNER'', ''SALESMAN'', 7698, TO_DATE(''08-09-1981'', ''dd-mm-yyyy''), 1500.00, 0.00, 30)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7876, ''ADAMS'', ''CLERK'', 7788, TO_DATE(''23-05-1987'', ''dd-mm-yyyy''), 1100.00, NULL, 20)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7902, ''FORD'', ''ANALYST'', 7566, TO_DATE(''03-12-1981'', ''dd-mm-yyyy''), 3000.00, NULL, 20)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7904, ''JAMES'', ''CLERK'', 7698, TO_DATE(''03-12-1981'', ''dd-mm-yyyy''), 950.00, NULL, 30)';
      EXECUTE IMMEDIATE 'INSERT INTO EMP VALUES (7934, ''MILLER'', ''CLERK'', 7782, TO_DATE(''23-01-1982'', ''dd-mm-yyyy''), 1300.00, NULL, 10)';
      COMMIT;
    END;
    DBMS_OUTPUT.PUT_LINE('STEP3,完成');
    --STEP4:添加约束
    BEGIN 
      EXECUTE IMMEDIATE 'ALTER TABLE EMP ADD CONSTRAINT PK_EMP PRIMARY KEY (EMPNO)';
      EXECUTE IMMEDIATE 'ALTER TABLE EMP ADD CONSTRAINT FK_DEPTNO FOREIGN KEY (DEPTNO) REFERENCES DEPT (DEPTNO)';
    END;
    DBMS_OUTPUT.PUT_LINE('STEP4,完成');
  END SP_REBUILD_EMP;

  --第二个程序
  PROCEDURE SP_YEAR(
  I_YEAR IN NUMBER,    --输入类型：IN
  O_YEAR OUT VARCHAR2)  --输出类型：OUT,如果此处的IN和OUT都不写，则全部默认为输入类型
  IS
  BEGIN
    IF MOD(I_YEAR,4)=0 AND MOD(I_YEAR,100)<>0 OR MOD(I_YEAR,400)=0 THEN
      O_YEAR:='闰年';
    ELSE
      O_YEAR:='平年';
    END IF;
  END;
  --第三个程序
  FUNCTION F_SUM (A NUMBER,B NUMBER)
  RETURN NUMBER
  AS
  S  NUMBER;
  BEGIN
    S := NVL(A,0)+NVL(B,0);
    RETURN S;
  END ;
END PKG73;
```



#### 3. 包建立时的注意点

- 1）**包头**和**包体**的名字要**相同**；
- 2）**包头**要先于**包体**建立，要分开建立；
- 3）**包头**和**包体**中只有建立包的**关键词**，没有建立**过程**、**函数**等的关键词；
- 4）**包头**中只需各个程序的**名字**和**参数信息**即可，不需要具体**内容**，而**包体**中除了**名字**和**参数信息**，也要写上具体**内容**。



思考：包中的过程和一般的过程是否能够重名？

　　　不能重名

